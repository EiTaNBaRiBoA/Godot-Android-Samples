plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk versions.compileSdk

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode 1
        versionName "1.0"
    }

    namespace = "fhuyakou.godot.plugin.android.helloworld"

    // Used to customize the name of generated AAR file.
    libraryVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            output.outputFileName = "HelloWorld.${variant.name}.aar"
        }
    }

}

dependencies {
    compileOnly libraries.godotAndroidLib
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlinVersion"
}

tasks.register('copyDebugAARToDemoAddons', Copy) {
    from 'build/outputs/aar'
    include 'HelloWorld.debug.aar'
    into 'demo/addons/hello_world_plugin/bin/debug'
}

tasks.register('copyReleaseAARToDemoAddons', Copy) {
    from 'build/outputs/aar'
    include 'HelloWorld.release.aar'
    into 'demo/addons/hello_world_plugin/bin/release'
}

tasks.register('copyAddonsToDemo', Copy) {
    finalizedBy(copyDebugAARToDemoAddons)
    finalizedBy(copyReleaseAARToDemoAddons)

    doFirst {
        delete('demo/addons/hello_world_plugin')
    }
    from 'src/main/assets/addons/hello_world_plugin'
    into 'demo/addons/hello_world_plugin'
}

assemble.finalizedBy(copyAddonsToDemo)
assemble.finalizedBy(copyDebugAARToDemoAddons)
assemble.finalizedBy(copyReleaseAARToDemoAddons)
